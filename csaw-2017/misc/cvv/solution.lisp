(eval-when (:compile-toplevel :execute)
  (ql:quickload '(:usocket :screamer :alexandria :iterate :cl-ppcre :cl-arrows)))

(in-package screamer-user)

(eval-when (:compile-toplevel :execute)
  (use-package '(:usocket :cl-ppcre :cl-arrows)))


(defparameter *issuer-prefixes*
  '(("Visa" (4))
    ("MasterCard" (5 1) (5 2) (5 3) (5 4) (5 5))
    ("American Express" (3 7))
    ("Discover" (6 0 1 1) (6 5))))

(defparameter *card-lengths*
  '(("Visa" . 16)
    ("MasterCard" . 16)
    ("American Express" . 15)
    ("Discover" . 16)))


(defun digit-sum (num)
  (iter:iter
    (iter:while (plusp num))
    (iter:sum (mod num 10))
    (setf num (floor num 10))))

(defun a-random-digit ()
  (a-member-of (alexandria:shuffle (alexandria:iota 10))))

(defun a-random-nonzero-digit ()
  (a-member-of (alexandria:shuffle (alexandria:iota 9 :start 1))))

(defun a-digit-list (length)
  (when (plusp length)
    (list* (a-random-digit) (a-digit-list (1- length)))))

(defun luhn-verify (card)
  (let* ((luhn-sum (loop :for digit :in (reverse card)
                         :for i :from 0
                         :for multiplier := (1+ (mod i 2))
                         :sum (digit-sum (* digit multiplier)))))
    (zerop (mod luhn-sum 10))))

(defun a-card (length &optional prefix)
  (let ((card (-> (append prefix
                          (list (a-random-nonzero-digit))
                          (a-digit-list (- length (length prefix) 1)))
                  (subseq 0 length))))
    (assert! (luhn-verify card))
    card))

(defun assert-suffix (suffix card)
  (mapcar (lambda (a b) (assert! (= a b)))
          (reverse suffix)
          (reverse card)))


(defmacro if-match-bind (vars regex string then else)
  (let ((result (gensym "RESULT")))
    `(let ((,result (nth-value 1 (scan-to-strings ,regex ,string))))
       (if (and ,result (every #'identity ,result))
           (let ,(loop :for var :in vars
                       :for i :from 0
                       :collect `(,var (aref ,result ,i)))
             ,then)
           ,else))))

(defmacro switch-match-bind (string &body cases)
  (when cases
    (if (eq t (caar cases))
        `(progn ,@(cdar cases))
        `(if-match-bind ,(caar cases) ,(cadar cases) ,string
             (progn ,@(cddar cases))
             (switch-match-bind ,string ,@(cdr cases))))))

(cl:defun main ()
  (let* ((s (socket-connect "misc.chal.csaw.io" 8308))
         (ss (socket-stream s)))
    (iter:iter
      (unless (iter:first-iteration-p)
        (write-line (read-line ss)))

      (iter:for line := (read-line ss))
      (write-line line)

      (switch-match-bind line
        ((prefix) "I need a new card that starts with (\\d+)!"
         (-<> (one-value (a-card 16 (map 'list #'digit-char-p prefix)))
              (format nil "窿季黩轸瀛扉铄黩轸瀛扉铄篌┅ㄦ矧沐秕麴豸篌┅è篚骀轼⑸铄邃铄汜蜾麒殂孱潴鏖翳ㄜ茕、ō季镱瀛鲠祯戾è汜蜾ㄡ汜蜾倍┅ㄡ篌弪舡篚骀轼磲ъ轶＇溟玳舡汨狎篚骀轼汜蜾汜蜾┅ㄦ矧磲铋窿季黩轸瀛扉铄黩轸瀛扉铄篌┅ㄦ矧沐秕麴豸篌┅è轶篚弪⑸铄邃铄ㄛ镰卺莴、ō季镱瀛鲠祯ㄡ汜蜾ㄣ潋ㄡ篌镢轶篚弪汜蜾戾铉翳螵呼弩＇羼踽飑ㄡ戾犷潋獒候犷滹憝屐ㄣ潋ㄡ篌镢轶篚弪轶篚弪痱彐轼弩呼弩＇羼踽飑┅┅ㄦ矧磲铋窿季黩轸瀛扉铄黩轸瀛扉铄篌┅ㄦ矧沐秕麴豸篌┅è汜钿殇狒濠⑸铄邃麸腩秣殒ㄜ茕轶鲠扉洹ōㄩㄡ钿倍戾铉翳汜钿殇狒濠祯桀鲥蜷纟磲ъ轶＇溟玳舡汨狎汜钿殇狒濠┅癌黩轸瀛麸篝蜷铉黩轸瀛扉铄黩轸瀛扉铄篌┅ㄦ矧沐秕麴豸篌黩轸瀛扉铄蝈徜扉铄篌┅è㈡灬琨蝈趱蝾扉铄┅换澎箦痱轭镲痼祜镳黩轸瀛扉铄蝈徜扉铄篌┅┅┅┅